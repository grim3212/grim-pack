// Based off of Pahimar's build file
// https://github.com/pahimar/Equivalent-Exchange-3/blob/master/build.gradle

// For those who want the bleeding edge
buildscript {
    repositories {
		mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            // Required for the curseforge task
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.0"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

version = minecraft_version + "-" + mod_version
group= mod_group
archivesBaseName = mod_archives_base_name

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = minecraft_version + "-" + forge_version
    runDir = "run"
	
	// Set mod version in code prior to compilation
    replaceIn "GrimPack.java"
    replace "@MOD_VERSION@", mod_version
	
    // Specify the mcp mappings to use
    mappings = mcp_mappings_version
}

repositories {
  maven {
    // location of the maven that hosts JEI files
    name = "Progwml6 maven"
    url = "http://dvs1.progwml6.com/files/maven"
  }
  maven {
    // location of a maven mirror for JEI files, as a fallback
    name = "ModMaven"
    url = "modmaven.k-4u.nl"
  }
  maven { // JustEnoughResources
        name = "bintray-way2muchnoise"
        url  "https://dl.bintray.com/way2muchnoise/dev/"
  }
}

dependencies {
  // compile against the JEI API
  deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
  // at runtime, use the full JEI jar
  runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
  
  provided "jeresources:JustEnoughResources-${minecraft_version}:${jer_version}:deobf"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def Properties props = new Properties()
if(new File('secrets.properties').isFile()){
	props.load(new FileInputStream(new File('secrets.properties')))
} else {
	println 'Secrets not found'
}

curseforge {
    apiKey = props.curseforge_apikey ? props.curseforge_apikey : ''
    project {
        id = props.grimpack_id ? props.grimpack_id : '0'
		mainArtifact jar
        releaseType = mod_release_type
        changelogType = 'html'
        changelog = file("changelog.html")
        relations {
            optionalLibrary 'jei'
			optionalLibrary 'just-enough-resources-jer'
        }
    }
}